plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	// Rest Docs (1) asciidoctor 추가
	id "org.asciidoctor.convert" version "1.5.9.2"
}

group = 'com.juno'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// https://mvnrepository.com/artifact/com.google.code.gson/gson
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
	// Querydsl
	implementation 'com.querydsl:querydsl-jpa'
	// Querydsl JPAAnnotationProcessor 사용 지정
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
	// Querydsl java.lang.NoClassDefFoundError(javax.annotation.Entity) 발생 대응
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	// Querydsl java.lang.NoClassDefFoundError(javax.annotation.Generated) 발생 대응
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	// Rest Docs (2) mockMvc에서 restdocs를 사용할 수 있도록 추가
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

// Rest Docs (3) 빌드시 snippets 파일들이 저장될 저장소
ext {
	snippetsDir = file('build/generated-snippets')
}

// Rest Docs (4) test 실행 시 파일을 (3)에서 설정한 저장소에 출력하도록 설정
test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

// Rest Docs (5) asccidoctor 설정
asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

// Rest Docs (7) bootJar 시 asciidoctor 종속되고 build하위 스니펫츠 파일을 classes 하위로 복사
bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5"){
		into 'static/docs'
	}
}
/*
// Rest Docs (8) from의 파일을 into로 복사
task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc/html5/")
	into file("src/main/resources/static/docs")
}

// Rest Docs (9) build 시 copyDocument 실행
build {
	dependsOn copyDocument
}
*/
